filename= 'time2.out'

parallel_color = "#FF3C00"
parallel_color_lazy = "#008CFF"
sequential_color = "#00993A"
lwidth = 3

number_of_cores = 8 
min_potenz = 7
max_potenz = 11

zahl_1 = 0.15
zahl_2 = 0.15

getGraphName(i) = (i == 1 || i == 3) ? "Cross Graph" : "Circle Graph"

set terminal eps size 6,4

set output 'weak_scaling.eps'

do for [thrp=0:0] {
	threshold = 2**(2*thrp)
	do for [r=10:10] {

		grid_size = 2**r

		#two plots per site
		set multiplot layout 1,2  title 'Weak scaling, threshold='.sprintf("%g", threshold)

		do for [grapht=1:2] {

			#functions
			filter(col, i, retcol) = (column(col) == i ? column(retcol) : 1/0)
			filter2(col, i, col2, j, retcol) = (column(col) == i && column(col2) == j ? column(retcol) : 1/0)
			filter3(col, i, col2, j, col3, k, retcol) = (column(col) == i && column(col2) == j && column(col3) == k ? column(retcol) : 1/0)
			filter4(col, i, col2, j, col3, k, col4, l, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(col4) == l ? column(retcol) : 1/0)

			set style data boxplot

			set title getGraphName(grapht)
			set xlabel offset 0,0 'number of cores / graph width'
			if (grapht==1) {
				set ylabel offset 2 'time [s]'
			} else {
				unset ylabel
			}

			#box style

			set pointsize 0.05
			set boxwidth 0.41
			set style fill  border lc rgb parallel_color

#			set logscale x 2
#			set logscale y 10

			set xrange [0.1:8.9] 
			set yrange [0.15:0.9]

			do for [i=1:number_of_cores] { set xtics add (sprintf("%g\n%g",i, int(grid_size*sqrt(i))) i) }
			do for [i=1:20] { set ytics add (i*0.2) }	
			set ytics add (0.3)
			set ytics add (0.5)
			set xtics font ",10" rotate by 45

			#erste Zeile ohne Titel!
			plot \
				for [i=2:number_of_cores] \
					filename using (i+zahl_1): \
					(filter4(1, grapht+2, 2, threshold, 5, i, 3, int(grid_size*sqrt(i)), 7)) \
					lc rgb parallel_color lw lwidth notitle, \
				filename using (1+zahl_1): \
					(filter4(1, grapht+2, 2, threshold, 5, 1, 3, grid_size, 7)) \
					lc rgb parallel_color lw lwidth title 'parallel normal', \
				for [i=2:number_of_cores] \
					filename using (i-zahl_2): \
					(filter4(1, grapht+2, 2, threshold, 5, i, 3, int(grid_size*sqrt(i)), 9)) \
					lc rgb parallel_color_lazy lw lwidth \
					fill empty border lc rgb parallel_color_lazy notitle, \
				filename using (1-zahl_2): \
					(filter4(1, grapht+2, 2, threshold, 5, 1, 3, grid_size, 9)) \
					lc rgb parallel_color_lazy lw lwidth fill empty \
					border lc rgb parallel_color_lazy title 'parallel lazy'

		}
		unset multiplot
	}
}
