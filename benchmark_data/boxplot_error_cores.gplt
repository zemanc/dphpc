filename= 'time1.out'

parallel_color = "#FF3C00"
parallel_color_lazy = "#008CFF"
sequential_color = "#00993A"
lwidth = 3

number_of_cores = 8 
min_potenz = 7
max_potenz = 11

zahl_1 = 0.13
zahl_2 = 0.13

getGraphName(i) = (i == 1 || i == 3) ? "Cross Graph" : "Circle Graph"

set terminal eps size 6,4

set output 'error_cores.eps'

do for [thrp=0:0] {
	threshold = 2**thrp
	do for [r=11:11] {

		grid_size = 2**r

		#two plots per site
		set multiplot layout 1,2  title 'relative Error, '.grid_size.' x '.grid_size.', threshold='.sprintf("%g", threshold)

		do for [grapht=1:2] {

			#calculate max and min for scale
			exact_result1 = system("awk 'NR==1{c=0;s=0} $1==1&&$5==-1&&$3==".grid_size."{s+=$9;c++} END{print s/c}' time1.out");
			exact_result2= system("awk 'NR==1{c=0;s=0} $1==2&&$5==-1&&$3==".grid_size."{s+=$9;c++} END{print s/c}' time1.out");
			maxy1 = (system("awk 'NR==1{max=0} $1==1&&$2==".sprintf("%g",threshold)."&&$3==".grid_size."&&$8>max{max=$8} $1==1&&$2==".sprintf("%g",threshold)."&&$3==".sprintf("%g",grid_size)."&&$10>max&&NF==10{max=$10} END{print max;}' time1.out")/exact_result1-1)*100
			maxy2 = (system("awk 'NR==1{max=0} $1==2&&$2==".sprintf("%g",threshold)."&&$3==".grid_size."&&$8>max{max=$8} $1==2&&$2==".sprintf("%g",threshold)."&&$3==".sprintf("%g",grid_size)."&&$10>max&&NF==10{max=$10} END{print max;}' time1.out")/exact_result2-1)*100
			if (grapht==1) {
				exact_result = exact_result1
			} else {
				exact_result = exact_result2
			}
			maxy = (maxy1 > maxy2) ? maxy1 : maxy2

			#functions
			filter(col, i, retcol) = (column(col) == i ? column(retcol) : 1/0)
			filter2(col, i, col2, j, retcol) = (column(col) == i && column(col2) == j ? column(retcol) : 1/0)
			filter3(col, i, col2, j, col3, k, retcol) = (column(col) == i && column(col2) == j && column(col3) == k ? column(retcol) : 1/0)
			filter4(col, i, col2, j, col3, k, col4, l, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(col4) == l ? column(retcol) : 1/0)

			set style data boxplot

			set title getGraphName(grapht) offset 0,-0.5
			set xlabel offset 0,0.5 'number of cores'
			if (grapht==1) {
				set ylabel offset 1.4,0 'error [%]'
				set lmargin 7
				set rmargin 0
				set tmargin 1.3

				do for [i=0:6] { set ytics add (sprintf("%.1f",0.5*i) 0.5*i) }

			} else {
				set lmargin 2
				set rmargin 5
				set tmargin 1.3
				unset ylabel
				unset ytics
				
				do for [i=0:6] { set ytics add (" " 0.5*i) }
				do for [i=0:6] { set y2tics add (sprintf("%.1f",0.5*i) 0.5*i) }
			}

			#box style

			set pointsize 0.05
			set boxwidth 0.31
			set style fill  border lc rgb parallel_color

			set xrange [0:8.8]
			set yrange [0:maxy*1.3]
			set y2range [0:maxy*1.3]

			do for [i=1:number_of_cores] { set xtics add (sprintf("%g",i) i) }

			#erste Zeile ohne Titel!
			plot \
				for [i=2:number_of_cores] \
					filename using (i-zahl_1): \
					((filter4(1, grapht, 2, threshold, 5, i, 3, grid_size, 8)/exact_result-1)*100) \
					lc rgb parallel_color lw lwidth notitle, \
				filename using (1-zahl_1): \
					((filter4(1, grapht, 2, threshold, 5, 1, 3, grid_size, 8)/exact_result-1)*100) \
					lc rgb parallel_color lw lwidth title 'parallel normal', \
				for [i=2:number_of_cores] \
					filename using (i+zahl_1): \
					((filter4(1, grapht, 2, threshold, 5, i, 3, grid_size, 10)/exact_result-1)*100) \
					lc rgb parallel_color_lazy lw lwidth \
					fill empty border lc rgb parallel_color_lazy notitle, \
				filename using (1+zahl_2): \
					((filter4(1, grapht, 2, threshold, 5, 1, 3, grid_size, 10)/exact_result-1)*100) \
					lc rgb parallel_color_lazy lw lwidth fill empty \
					border lc rgb parallel_color_lazy title 'parallel lazy', \
				filename using (1): \
					((filter4(1, grapht, 2, threshold, 5, -1, 3, grid_size, 8)/exact_result-1)*100) \
					lc rgb sequential_color lw lwidth fill empty \
					border lc rgb sequential_color title 'sequential'

		}
		unset multiplot
	}
}
