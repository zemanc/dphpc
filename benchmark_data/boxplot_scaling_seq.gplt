#OK 24.12
filename= 'time1.out'

sequential_color = "#008CFF"
lwidth = 5

number_of_cores = 8 
min_potenz = 7
max_potenz = 11

zahl_1 = 1.01
zahl_2 = 0.98

getGraphName(i) = (i == 1 || i == 3) ? "Cross Graph" : "Circle Graph"

set key font ",15"
set terminal eps size 6,4

set output 'seq_scaling.eps'

do for [thrp=0:0] {
	threshold = 2**thrp

	#two plots per site
	set multiplot layout 1,2  title 'Scaling sequential, threshold='.sprintf("%g", threshold) font ",17"

	#calculate max and min for scale
	maxy = system("awk 'NR==1{max=0} $1<=2&&$2==".int(sprintf("%g",threshold))."&&$5+1==0&&$7>max{max=$7} END{print max;}' time1.out")
	miny = system("awk 'NR==1{min=10000} $1<=2&&$2==".sprintf("%g",threshold)."&&$5==-1&&$7<min{min=$7} END{print min;}' time1.out")

	do for [grapht=1:2] {

		#functions
		filter(col, i, retcol) = (column(col) == i ? column(retcol) : 1/0)
		filter2(col, i, col2, j, retcol) = (column(col) == i && column(col2) == j ? column(retcol) : 1/0)
		filter3(col, i, col2, j, col3, k, retcol) = (column(col) == i && column(col2) == j && column(col3) == k ? column(retcol) : 1/0)
		filter4(col, i, col2, j, col3, k, col4, l, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(col4) == l ? column(retcol) : 1/0)

		set style data boxplot

		set title getGraphName(grapht) offset 0,-0.5 font ",15"
		set xlabel offset 0,0.5 'width of graph' font ",15"
		if (grapht==1) {
			set ylabel offset 3.8,0 'time [s]' font ",15"
			set lmargin 6
			set rmargin 1
			set tmargin 1.3
			set ytics offset 0.7,0 font ",15"

		} else {
			set lmargin 1
			set rmargin 6
			set tmargin 1.3
			unset ylabel
			do for[i in "0.001 0.01 0.1 1"] { set ytics add (" " i) font ",15"}
			set y2tics offset -1.1,0 font ",15"
		}

		#box style

		set pointsize 0.05
		set boxwidth 0.35

		set logscale y 10
		set logscale y2 10
		set logscale x 2

		set xtics font ",15"

		set xrange [2**min_potenz*0.7:2**max_potenz/0.7]
		set yrange [miny*0.7:maxy/0.7]
		set y2range [miny*0.7:maxy/0.7]
		set nokey

		#erste Zeile ohne Titel!
		plot for [i=min_potenz:max_potenz] \
				filename using (2**i): \
				(filter4(1, grapht, 2, threshold, 5, -1, 3, 2**i, 7)) \
				lc rgb sequential_color lw lwidth fill empty \
				border lc rgb sequential_color notitle

	}
	unset multiplot
}
