filename= 'time1.out'

parallel_color = "#FF3C00"
parallel_color_lazy = "#008CFF"
parallel_color2 = "#EE8E00"
parallel_color_lazy2 = "#33B3AA"
sequential_color = "#00993A"
lwidth = 5

set key font ",15"

number_of_cores = 4 
min_potenz = 7
max_potenz = 11

zahl_1 = 1.03
zahl_2 = 0.98

min_thr_pot = -3
max_thr_pot = 3

p = 2

getGraphName(i) = (i == 1 || i == 3) ? "Cross Graph" : "Circle Graph"

set terminal eps size 6,4

set output 'speedup_threshold.eps'

do for [r=11:11] {

	grid_size = 2**r

	#two plots per site
	set multiplot layout 1,2  title 'speedup vs. sequential, '.grid_size.' x '.grid_size font ",17"

	do for [grapht=1:2] {

		#calculate max and min for scale
		exact_result1 = system("awk 'NR==1{c=0;s=0} $1==1&&$5==-1&&$3==".grid_size."{s+=$9;c++} END{print s/c}' time1.out");
		exact_result2= system("awk 'NR==1{c=0;s=0} $1==2&&$5==-1&&$3==".grid_size."{s+=$9;c++} END{print s/c}' time1.out");
		maxy1 = (system("awk 'NR==1{max=0} $1==1&&$3==".grid_size."&&$8>max{max=$8} $1==1&&$3==".sprintf("%g",grid_size)."&&$10>max&&NF==10{max=$10} END{print max;}' time1.out")/exact_result1-1)*100
		maxy2 = (system("awk 'NR==1{max=0} $1==2&&$3==".grid_size."&&$8>max{max=$8} $1==2&&$3==".sprintf("%g",grid_size)."&&$10>max&&NF==10{max=$10} END{print max;}' time1.out")/exact_result2-1)*100
		if (grapht==1) {
			exact_result = exact_result1
		} else {
			exact_result = exact_result2
		}
		maxy = (maxy1 > maxy2) ? maxy1 : maxy2
		miny1= (system("awk 'NR==1{min=100000} $1==1&&$3==".grid_size."&&$8<min{min=$8} $1==1&&$3==".sprintf("%g",grid_size)."&&$10<min&&NF==10{min=$10} END{print min;}' time1.out")/exact_result1-1)*100
		miny2= (system("awk 'NR==1{min=100000} $1==2&&$3==".grid_size."&&$8<min{min=$8} $1==2&&$3==".sprintf("%g",grid_size)."&&$10<min&&NF==10{min=$10} END{print min;}' time1.out")/exact_result2-1)*100
		miny= (miny1 < miny2) ? miny1 : miny2 

		#functions
		mean4(col, i, col2, j, col3, k, col4, l, retcol) = (system("awk 'NR==1{sum=0;cnt=0} $".col."==".i."&&$".col2."==".sprintf("%g",j)."&&$".col3."==".sprintf("%g",k)."&&$".col4."==".sprintf("%g",l)."{sum+=$".sprintf("%g",retcol).";cnt+=1} END{print sum/cnt}' time1.out"))
		filter(col, i, retcol) = (column(col) == i ? column(retcol) : 1/0)
		filter2(col, i, col2, j, retcol) = (column(col) == i && column(col2) == j ? column(retcol) : 1/0)
		filter3(col, i, col2, j, col3, k, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(5)>=0 ? column(retcol) : 1/0)
		filter4(col, i, col2, j, col3, k, col4, l, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(col4) == l ? column(retcol) : 1/0)
		filter4d(col, i, col2, j, col3, k, col4, l, retcol) = (column(col) == i && column(col2) == j && column(col3) == k && column(col4) == l ? (mean4(col, i, col2, j, 3, grid_size, 5, -1, 7)/column(retcol)) : 1/0)

		set style data boxplot

		set title getGraphName(grapht) font ",15" offset 0,-0.5 
		set xlabel offset 0,0.5 'threshold relaxing' font ",15"
		if (grapht==1) {
			set ylabel offset 1.4,0 'speedup' font ",15"
			set lmargin 7
			set rmargin 0
			set tmargin 1.3
			do for [i=1:10] { set ytics add (sprintf("%.1f",0.5*i) 0.5*i) font ",15" }

		} else {

			set lmargin 2
			set rmargin 5
			set tmargin 1.3
			unset ylabel
			unset ytics

			do for [i=1:10] { set ytics add ("" 0.5*i) }
			do for [i=1:10] { set y2tics add (sprintf("%.1f",0.5*i) 0.5*i) font ",15"}
		}
		show margin;

		#box style
		set logscale x 2

		set pointsize 0.05
		set boxwidth 0.11
		set style fill  border lc rgb parallel_color

		set xrange [0.08:11]
		set yrange[0.4:4.8]
		set y2range[0.4:4.8]

		do for [i=min_thr_pot:max_thr_pot] { 
			set xtics add (sprintf("%g",2**i) 2**i)  font ",15"
		}

		#erste Zeile ohne Titel!
		plot \
			for [i=min_thr_pot+1:max_thr_pot] \
				filename using (2**i): \
				(filter4d(1, grapht, 2, 2**i, 3, grid_size, 5, p, 7)) \
				lc rgb parallel_color2 lw lwidth notitle, \
			filename using (2**min_thr_pot): \
				(filter4d(1, grapht, 2, 2**min_thr_pot, 3, grid_size, 5, p, 7)) \
				lc rgb parallel_color2 lw lwidth title 'parallel normal 2 cores', \
			for [i=min_thr_pot+1:max_thr_pot] \
				filename using (2**i): \
				(filter4d(1, grapht, 2, 2**i, 3, grid_size, 5, 2*p, 7)) \
				lc rgb parallel_color lw lwidth notitle, \
			filename using (2**min_thr_pot): \
				(filter4d(1, grapht, 2, 2**min_thr_pot, 3, grid_size, 5, 2*p, 7)) \
				lc rgb parallel_color lw lwidth title 'parallel normal 4 cores', \
			for [i=min_thr_pot+1:max_thr_pot] \
				filename using (2**i): \
				(filter4d(1, grapht, 2, 2**i, 3, grid_size, 5, 4*p, 7)) \
				lc rgb sequential_color lw lwidth notitle, \
			filename using (2**min_thr_pot): \
				(filter4d(1, grapht, 2, 2**min_thr_pot, 3, grid_size, 5, 4*p, 7)) \
				lc rgb sequential_color lw lwidth title 'parallel normal 8 cores'

	}
	unset multiplot
}
