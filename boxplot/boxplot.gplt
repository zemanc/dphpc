filename_parallel = 'time_p_circlegraph.out'
filename_sequential = 'time_s_circlegraph.out'

parallel_color = "#FF8C00"
sequential_color = "#00993A"
lwidth = 5

number_of_cores = 8 

set terminal eps size 6,4

set output 'out.eps'

do for [r=7:12] {
	
	grid_size = 2**r

	#functions
	filter(col, i, retcol) = (column(col) == i ? column(retcol) : 1/0)
	filter2(col, i, col2, j, retcol) = (column(col) == i && column(col2) == j ? column(retcol) : 1/0)

	set style data boxplot

	set title 'Strong scaling '.grid_size.' x '.grid_size
	set xlabel 'number of cores'
	set ylabel 'time [s]'

	#box style

	set pointsize 0.05
	set boxwidth 0.11
	set style fill solid 0.2 border lc rgb parallel_color

	set logscale xy 2

	set xrange [0.9:8.8]
	set xtics (1,2,3,4,5,6,7,8)

	#erste Zeile ohne Titel!
	plot for [i=2:number_of_cores] filename_parallel using (i):(filter2(3, i, 1, grid_size, 5)) lc rgb parallel_color lw lwidth notitle, \
		filename_parallel using (1):(filter2(3, 1, 1, grid_size, 5)) lc rgb parallel_color lw lwidth title 'parallel', \
		filename_sequential using (1):(filter(1, grid_size, 4)) lc rgb sequential_color lw lwidth fill solid 0.2 border lc rgb sequential_color title 'sequential'

}
